MODULE main
VAR
	x   : 1 .. 2;
	pg1 : peterson(1, x, pg2.b);
	pg2 : peterson(2, x, pg1.b);
ASSIGN
	next(x) := case
			(pg1.state = wait)  : 2;
			(pg2.state = wait)  : 1;
			TRUE                : x;
		esac;

FAIRNESS
	pg1.state = crit
FAIRNESS
	pg2.state = crit


LTLSPEC G !(pg1.state = crit & pg2.state = crit)




MODULE peterson(id, x, other_b)
VAR
	state : { noncrit, wait, crit };
ASSIGN
	init(state) := noncrit;
	next(state) := case
			(state = noncrit)                        : { noncrit, wait };
			(state = wait) & ((id = x) | !(other_b)) : { wait, crit };
			(state = crit)                           : { crit, noncrit };
			TRUE                                     : state;
		esac;
DEFINE
	b := (state = wait) | (state = crit);